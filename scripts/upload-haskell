#!/usr/bin/perl

# Copyright (c) 2012 Joe Leslie-Hurd, distributed under the MIT license

use strict;
use warnings;
use Pod::Usage;
use Getopt::Std;

###############################################################################
# Process options.
###############################################################################

use vars qw($opt_h $opt_n);

getopts('hn');

if ($opt_h) {
    pod2usage({-exitval => 2,
	       -verbose => 2});
}

# Autoflush STDIN
$|++;

###############################################################################
# Global variables.
###############################################################################

my $program = "upload-haskell";

my $opentheory = "bin/mlton/opentheory";
[ -x $opentheory ] or die "$program: $opentheory not an executable";

my $source = "data/haskell";
[ -d $source ] or die "$program: $source not a directory";

###############################################################################
# Helper functions.
###############################################################################

sub compare_version {
  (scalar @_ == 2) or die;
  my $version1 = shift @_;
  my $version2 = shift @_;

  my $v1 = "." . $version1;
  my $v2 = "." . $version2;

  while(1) {
    if ($v1 eq '') {
      if ($v2 eq '') {
        return 0;
      }
      else {
        return -1;
      }
    }
    else {
      if ($v2 eq '') {
        return 1;
      }
      else {
        $v1 =~ /^[.]([[:digit:]]+)(.*)$/ or
          die "$program: bad version format: $version1\n";
        my $c1 = $1;
        $v1 = $2;

        $v2 =~ /^[.]([[:digit:]]+)(.*)$/ or
          die "$program: bad version format: $version2\n";
        my $c2 = $1;
        $v2 = $2;

        if ($c1 < $c2) {
          return -1;
        }
        elsif ($c2 < $c1) {
          return 1;
        }
      }
    }
  }
}

sub name_version_to_name {
  (scalar @_ == 1) or die;
  my $nv = shift @_;

  if ($nv =~ m{^([[:alnum:]-]+)-([[:digit:].]+)$}) {
    return $1;
  }
  else {
    die "$program: not a valid NAME-VERSION: $nv";
  }
}

sub name_version_to_version {
  (scalar @_ == 1) or die;
  my $nv = shift @_;

  if ($nv =~ m{^([[:alnum:]-]+)-([[:digit:].]+)$}) {
    return $2;
  }
  else {
    die "$program: not a valid NAME-VERSION: $nv";
  }
}

sub name_version_to_haskell_name {
  (scalar @_ == 1) or die;
  my $nv = shift @_;

  my $name = '';

  my $cmd = "$opentheory info --information $nv";

  open INFO, "$cmd |" or
    die "$program: couldn't get information for theory $nv: $!\n";

  while (my $info = <INFO>) {
    chomp $info;

    if ($info =~ /^haskell-name:[[:space:]]+([[:alnum:]-]+)$/) {
      if ($name eq '') {
        $name = $1;
      }
      else {
        die ("$program: multiple haskell-name information in theory $nv:\n" .
             "  $name and $2\n");
      }
    }
  }

  close INFO;

  if ($name eq '') {
    $name = 'opentheory-' . name_version_to_name($nv);
  }

  return $name;
}

sub haskell_name_to_version {
  (scalar @_ == 1) or die;
  my $name = shift @_;

  my $version = '';

  my $cabal = "$source/$name/$name.cabal";

  open INFO, "$cabal" or
    die "$program: couldn't open cabal file $cabal: $!\n";

  while (my $info = <INFO>) {
    chomp $info;

    if ($info =~ /^version:[[:space:]]+([[:digit:].]+)$/) {
      if ($version eq '') {
        $version = $1;
      }
      else {
        die ("$program: multiple version tags in cabal file $cabal:\n" .
             "  $version and $1\n");
      }
    }
  }

  close INFO;

  if ($version eq '') {
    die "$program: no version information found in cabal file $cabal\n";
  }

  return $version;
}

###############################################################################
# Main program.
###############################################################################

my $cmd;

$cmd = "$opentheory --repo gilith update";
if ($opt_n) { print STDOUT "$cmd\n"; }
if (system($cmd) != 0) { die "$program: couldn't run $cmd"; }

$cmd = "cabal update";
if ($opt_n) { print STDOUT "$cmd\n"; }
if (system($cmd) != 0) { die "$program: couldn't run $cmd"; }

my $skipping = '';
my $staging = '';
my $upload_cmd = 'cabal upload';

$cmd = "$opentheory --repo gilith list --dependency-order 'Mine /\\ ExportHaskell /\\ UpToDate'";
if ($opt_n) { print STDOUT "$cmd\n"; }

open THEORIES, "$cmd |" or
  die "$program: couldn't list installed theories: $!\n";

while (my $thy = <THEORIES>) {
  chomp $thy;

  $cmd = "$opentheory --repo gilith list --quiet 'IdenticalOnRepo $thy'";
  if ($opt_n) { print STDOUT "$cmd\n"; }
  if (system($cmd) != 0) { die "$program: $thy is not on the gilith repo\n"; }

  my $name = name_version_to_haskell_name($thy);
  my $version = haskell_name_to_version($name);
  my $name_version = $name . '-' . $version;
  ($version eq name_version_to_version($thy)) or
    die "$program: opentheory package and Haskell package have different versions:\n  $thy vs $name_version\n";

  my $tar = "$source/$name/dist/$name_version.tar.gz";
  [ -f $tar ] or die "$program: couldn't find source for $name\n";

  $cmd = "cabal info $name";
  if ($opt_n) { print STDOUT "$cmd\n"; }

  open INFO, "$cmd |" or
    die "$program: couldn't execute $cmd: $!\n";

  my $latest_available = "";
  my $latest_installed = "";
  my $looking_for_latest = 0;

  while (my $info = <INFO>) {
    chomp $info;

    if ($looking_for_latest != 0) {
      die "unsupported";
    }

    if ($info =~ /^[[:space:]]*Versions available:[[:space:]]+(.*)$/) {
      my $vs = $1;

      if ($latest_available ne '') {
        die "$program: multiple 'Versions available' lines in cabal info $name\n";
      }

      if ($vs eq "[ Not available from server ]") {
        $latest_available = "0";
      }
      elsif ($vs =~ /,$/) {
        $looking_for_latest = 1;
      }
      elsif ($vs =~ /([[:digit:].]+)( [(]and [[:digit:]]+ others[)])?$/) {
        $latest_available = $1;
      }
      else {
        die "$program: couldn't parse 'Versions available' info:\n  $vs\n";
      }
    }

    if ($info =~ /^[[:space:]]*Versions installed:[[:space:]]+(.*)$/) {
      my $vs = $1;

      if ($latest_installed ne '') {
        die "$program: multiple 'Versions installed' lines in cabal info $name\n";
      }

      if ($vs =~ /,$/) {
        $looking_for_latest = 2;
      }
      elsif ($vs =~ /([[:digit:].]+)( [(]and [[:digit:]]+ others[)])?$/) {
        $latest_installed = $1;
      }
      else {
        die "$program: couldn't parse 'Versions installed' info:\n  $vs\n";
      }
    }
  }

  close INFO;

  if ($latest_available eq '') {
    die "$program: no 'Versions available' lines found in cabal info $name\n";
  }

  if ($latest_installed eq '') {
    die "$program: no 'Versions installed' lines found in cabal info $name\n";
  }

  if ($latest_installed ne $version) {
    die "$program: latest 'Versions installed' line in cabal info $name does not match cabal file\n";
  }

  my $cmp = compare_version($latest_available, $latest_installed);
  if ($cmp > 0) {
    die "$program: for package $name, latest available version ($latest_available) is later than latest installed ($latest_installed)\n";
  }
  elsif ($cmp < 0) {
    print STDOUT "Staging $name_version\n";
    $staging .= "\n  $name_version";
    $upload_cmd .= " $tar";
  }
  else {
    print STDOUT "Skipping $name_version\n";
    $skipping .= "\n  $name_version";
  }
}

close THEORIES;

if ($skipping ne '') {
  print STDOUT "\nSkipping the following Haskell packages:$skipping\n";
}

if ($staging ne '') {
  print STDOUT "\nUploading the following Haskell packages:$staging\n\n";

  if ($opt_n) {
    print STDOUT "$upload_cmd\n";
  }
  else {
    if (system($upload_cmd) != 0) {
      die "$program: couldn't execute $upload_cmd: $!\n";
    }
  }
}

__END__

=pod

=head1 NAME

upload-haskell - Upload the auto-generated Haskell packages

=head1 SYNOPSIS

upload-haskell [-h] [-n]

=head1 ARGUMENTS

The recognized flags are described below:

=over 2

=item B<-h>

Produce this documentation.

=item B<-n>

Don't do anything destructive, just print the commands that would be used.

=back

=head1 DESCRIPTION

Uploads the verified Haskell packages that were generated from
OpenTheory packages to the Hackage repo.

=head1 BUGS

Waiting to rear their ugly heads.

=head1 AUTHORS

Joe Leslie-Hurd <joe@gilith.com>

=head1 SEE ALSO

Perl(1).

=cut
