#!/usr/bin/perl

# Copyright (c) 2011 Joe Hurd, All Rights Reserved

use strict;
use warnings;
use Pod::Usage;
use Getopt::Std;

use vars qw($opt_h $opt_u);

getopts('hu');

if ($opt_h or scalar @ARGV != 0)
{
    pod2usage({-exitval => 2,
	       -verbose => 2});
}

# Autoflush STDIN
$|++;

my $orderthys = "scripts/order-theories";
[ -x $orderthys ] or die "install-theories: $orderthys not an executable";

my $opentheory = "bin/mlton/opentheory";
[ -x $opentheory ] or die "install-theories: $opentheory not an executable";

my $bumpver = "scripts/bump-theory-version";
[ -x $bumpver ] or die "install-theories: $bumpver not an executable";

my $source = "data/theories";
[ -d $source ] or die "install-theories: $source not a directory";

my $cmd = "$orderthys";

# print STDOUT "cmd = $cmd\n";

open THEORIES, "$cmd |" or
  die "install-theories: couldn't execute $cmd: $!\n";

while (my $name = <THEORIES>) {
    chomp $name;

    my $thy = $source . '/' . $name . '/' . $name . '.thy';

    $cmd = "$opentheory info --format VERSION $thy";
    my $version = `$cmd`;
    chomp $version;

    $cmd = "$opentheory info --format CHECKSUM $name-$version &> /dev/null";
    # print STDOUT "cmd = $cmd\n";
    my $installed = (system ($cmd) == 0);

    if ($opt_u) {
        my $thynew = $thy . '.upgrading';

        $cmd = "$opentheory upgrade $thy >$thynew 2>/dev/null";
        # print STDOUT "cmd = $cmd\n";
        if (system ($cmd) == 0) {
            if ($installed) {
                $cmd = "$bumpver $thynew > $thy";
                (system ($cmd) == 0) or
                    die "install-theories: couldn't execute $cmd: $!\n";

                unlink $thynew or
                    die "install-theories: couldn't remove $thynew: $!\n";

                $installed = 0;
            }
            else {
                rename($thynew,$thy) or
                    die "install-theories: couldn't rename $thynew to $thy: $!\n";
            }
        }
        else {
            unlink $thynew or
                die "install-theories: couldn't remove $thynew: $!\n";
        }
    }

    if (!$installed) {
        $cmd = "$opentheory install --manual --reinstall --auto-uninstall $thy";
        (system ($cmd) == 0) or
            die "install-theories: couldn't execute $cmd: $!\n";
    }
}

close THEORIES;

__END__

=pod

=head1 NAME

install-theories - install theories in the local directory

=head1 SYNOPSIS

install-theories [-hu]

=head1 ARGUMENTS

The recognized flags are described below:

=over 2

=item B<-h>

Produce this documentation.

=item B<-u>

Upgrade the theories before installing them.

=back

=head1 DESCRIPTION

Install theories in the local directory.

=head1 BUGS

Waiting to rear their ugly heads.

=head1 AUTHORS

Joe Hurd <joe@gilith.com>

=head1 SEE ALSO

Perl(1).

=cut
