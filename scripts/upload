#!/usr/bin/perl

# Copyright (c) 2006 Joe Hurd, All Rights Reserved

use strict;
use warnings;
use Pod::Usage;
use Getopt::Std;

use vars qw($opt_h $opt_m $opt_c $opt_d $opt_n);

getopts('hmcdn');

if ($opt_h) {
    pod2usage({-exitval => 2,
	       -verbose => 2});
}

# Autoflush STDIN
$|++;

my @EXCLUSIONS =
   ('.DS_Store',
    '.gitignore');

my $user = "gilith";
my $host = "login.gilith.com";

sub upload_dir {
    (scalar @_ == 3) or die;
    my $local_dir = shift @_;
    my $remote_dir = shift @_;
    my $exclusions = shift @_;

    my $cmd = "rsync -azv -e ssh";
    $cmd .= " --delete" unless $opt_m;
    $cmd .= " --checksum" if $opt_c;
    $cmd .= " --size-only" unless $opt_d;

    for my $exclusion (@EXCLUSIONS) {
        $cmd .= " --exclude=$exclusion";
    }

    for my $exclusion (@{$exclusions}) {
        $cmd .= " --exclude=$exclusion";
    }

    $cmd .= " " . $local_dir . "/";
    $cmd .= " " . $user . '@' . $host . ":" . $remote_dir;

    if ($opt_n) {
        print "$cmd\n";
    }
    else {
        (system ($cmd) == 0) or
            die "upload: couldn't execute command:\n$cmd\n$!\n";
    }
}

my @repo_exclusions =
   ();

my $repo_remote_dir = "public_opentheory";
upload_dir "repo", $repo_remote_dir, \@repo_exclusions;

__END__

=pod

=head1 NAME

upload - Upload the Gilith OpenTheory repo scripts.

=head1 SYNOPSIS

upload [-h] [-m] [-c] [-d] [-n]

=head1 ARGUMENTS

The recognized flags are described below:

=over 2

=item B<-h>

Produce this documentation.

=item B<-m>

Don't delete any files from the live site.

=item B<-c>

Checksum to ensure the server has the most recent files (slow).

=item B<-d>

Compare file dates as well as sizes.

=item B<-n>

Do nothing, just echo the command that would be used.

=back

=head1 DESCRIPTION

Uploads the OpenTheory repo scripts to the live Gilith OpenTheory repo,
ignoring auxiliary files such as .gitignore files.

=head1 BUGS

Waiting to rear their ugly heads.

=head1 AUTHORS

Joe Hurd <joe@gilith.com>

=head1 SEE ALSO

Perl(1).

=cut
